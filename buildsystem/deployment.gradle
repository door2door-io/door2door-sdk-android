apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

artifactory {
    contextUrl = rootProject.artifactoryUrl
    publish {
        repository {
            repoKey = rootProject.computedVersion.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
            username = rootProject.artifactoryUser
            password = rootProject.artifactoryPassword
            maven = true
        }
        defaults {
            publishArtifacts = true
            publications('aar')
            publishIvy = false
        }
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = project.groupId
            artifactId project.artifactId
            version = rootProject.computedVersion
            artifact(sourceJar)
            artifact(androidJavadocsJar)
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

artifactoryPublish.dependsOn build

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    baseName = artifactId
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = project.artifactId
    from android.sourceSets.main.java.srcDirs
}